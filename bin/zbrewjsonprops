/* REXX */
/* 
 * Parse a JSON stream and write out a set of key/value pairs (one per line)
 * See: http://tech.mikefulton.ca/WebEnablementToolkit for details on the REXX JSON parsing services
 */

trace 'o'
Parse arg opts .

if (opts <> '') then do
  call SayErr 'Syntax: zbrewjson'
  call SayErr '  The JSON key/value pairs are read in from stdin'
  call SayErr '  The parsed key/value pairs are written out to stdout'
  return 4
end

call hwtcalls "on"

diagArea. = ''

address hwtjson "hwtConst ",
  "returnCode ",
  "diagArea."
if (rc <> 0 | returnCode <> HWTJ_OK) then do
  call Error 'Internal Error: Unable to get HWTJSON constants', diagArea.
  return 16
end

address hwtjson "hwtjinit ",
  "returnCode ",
  "parserHandle ",
  "diagArea."
if (rc <> 0 | returnCode <> HWTJ_OK) then do
  call Error 'Internal Error: Unable to initialize HWT', diagArea.
  return 16
end

address mvs 'execio * diskr stdin (stem data. finis'
stream=''
do i=1 to data.0
  stream=stream||data.i
end

address hwtjson "hwtjpars ",
  "returnCode ",
  "parserHandle ",
  "stream ",
  "diagArea." 
parseRC=rc

if (rc <> 0 | returnCode <> HWTJ_OK) then do
  call Error 'JSON Input is Invalid.', diagArea.
  return 8
end

objectHandle=0
objectEntryIndex=0
address hwtjson "hwtjgoen",
  "returnCode",        
  "parserHandle",                
  "objectHandle",            
  "objectEntryIndex",                      
  "entryName",              
  "entryValueHandle",
  "diagArea."
if (rc <> 0 | returnCode <> HWTJ_OK) then do
  call Error 'Internal Error: Unable to retrieve root JSON object.', diagArea.
  return 16
end

address hwtjson "hwtjgnue",
  "returnCode",
  "parserHandle",
  "objectHandle",
  "numOfEntries",
  "diagArea."
if (rc <> 0 | returnCode <> HWTJ_OK) then do
  call Error 'Internal Error: Unable to retrieve number of key/value pairs.', diagArea.
  return 16
end

maxRC=0
do objectEntryIndex=0 to numOfEntries-1
  address hwtjson "hwtjgoen",
    "returnCode",
    "parserHandle",
    "objectHandle",
    "objectEntryIndex",
    "entryName",
    "entryValueHandle",
    "diagArea."
  if (rc <> 0 | returnCode <> HWTJ_OK) then do
    call Error 'Internal Error: Unable to retrieve key/value pair entry: ' || objectEntryIndex, diagArea.
    return 16
  end

  address hwtjson "hwtjgjst",
    "returnCode",
    "parserHandle",
    "entryValueHandle",
    "entryValueType",
    "diagArea."
  if (rc <> 0 | returnCode <> HWTJ_OK) then do
    call Error 'Internal Error: Unable to retrieve type for key/value pair entry: ' || objectEntryIndex, diagArea.
    return 16
  end
  if (entryValueType <> 'HWTJ_STRING_TYPE') then do
    call SayErr 'Key: ' || entryName || ' is not of string type. Only string values can be specified. Key ignored.'
    maxRC=4
    iterate;
  end         

  address hwtjson "hwtjgval",
    "returnCode",
    "parserHandle",
    "entryValueHandle",
    "entryValue",
    "diagArea."
  if (rc <> 0 | returnCode <> HWTJ_OK) then do
    call Error 'Unable to retrieve value for key/value pair entry: ' || objectEntryIndex, diagArea.
    return 16
  end

  Say entryName || '="' || entryValue || '"'
end

return maxRC

SayErr: Procedure
  Parse Arg text
  data.1 = text
  data.0 = 1
  address mvs 'execio 1 diskw stderr (stem data.' 
  return 0

Error: Procedure Expose diagArea.
Parse Arg msg, diagArea
  call SayErr msg
  call SayErr diagArea.HWTJ_ReasonDesc 
  return 0
